// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2" }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath Plugins.android_gradle
        classpath Plugins.kotlin_gradle
        classpath Plugins.versions_gradle
        classpath Plugins.google_services_gradle
        classpath Plugins.fabric

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply from: rootProject.file('gradle/versioning.gradle')

allprojects {
    group "de.psdev.devdrawer"
    version enhancedVersion(2, 0, 1)

    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    project.ext {
        // Project versions
        gitHash = gitScmVersion()
        appVersionName = project.version
        appVersionCode = buildVersionCode()
    }
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['jre', 'alpha', 'rc', 'beta'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

wrapper {
    gradleVersion = '5.6.2'
    distributionType = "all"
}